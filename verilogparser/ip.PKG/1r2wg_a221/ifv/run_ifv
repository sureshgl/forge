#!/bin/csh -f

if($#argv == 0) then
    echo "Usage: $0 <top_module_name> [-noob]"
    echo "   this runs only from <cut-dir>/run_sample/<cut-name> dir"
    echo "   -noob : use this if cut was generated with -noob"
    exit (1);
endif
echo "cmdline: $0 $argv"
set TOP=$argv[1]
set NOOB=0
if ($#argv == 2) then
    if ($argv[2] == '-noob') then
        set NOOB=1
    endif
endif
set SCRIPT=`readlink -f "$0"`
set SCRIPTPATH=`dirname "$SCRIPT"`
set BINPATH=`dirname "$0"| sed 's:verilogparser/.*$:verilogparser:'`
set BINDIR=`readlink -f "$BINPATH"`
set IPDIR="$BINDIR/ip_obfuscated"

set TOPV="../../../rtl/${TOP}.v"
set DESLIB=`\ls ${IPDIR}/common/rtl/memoir_design_library*.v`
if ($NOOB == 1) then
   set DESLIB=`\ls ${IPDIR}/memoir_design_library*.v`
endif
echo "design library used: $DESLIB"

set TCLA="+tcl+${SCRIPTPATH}"
set CMDP="iev +64bit -F filelist +define+FORMAL +licq +exit +top+${TOP}"

\rm -fr INCA_libs filelist

echo $SCRIPTPATH
echo $TOPV >! filelist
echo "$DESLIB" >> filelist

alias run '\rm -f \!:2.log; echo $CMDP ${TCLA}/\!:1.tcl -l \!:2.log; $CMDP ${TCLA}/\!:1.tcl -l \!:2.log;'

run show_all show_all

setenv IFV_HIER des.algo_top.a1_loop.algo

run 1r2w_rl2_mem_check mem_check
run 1r2w_rl2_mem_range_check mem_range_check
run 1r2w_rl2_ecc_check ecc_check
run 1r2w_rl2_smem_check smem_check
run 1r2w_rl2_forward_check forward_check
run 1r2w_rl2_rmem_check rmem_check
run 1r2w_rl2_vdout_int_check vdout_int_check
run 1r2w_rl2_fakemem_check fakemem_check
run 1r2w_rl2_dout_check dout_check

setenv IFV_HIER des.algo_top.t1_loop\[\*\].align_loop.infra

run t1_align_check t1_align_check
run t1_align_dout_check t1_align_dout_check

setenv IFV_HIER des.algo_top.t2_loop\[\*\].align_loop.infra

run t2_align_check t2_align_check.
run t2_align_dout_check t2_align_dout_check

setenv IFV_HIER des.algo_top.t2_loop\[\*\].stack_loop.infra

run stack_check t2_stack_check
run stack_dout_check t2_stack_dout_check

setenv IFV_HIER des.algo_top.t2_loop\[\*\].align_loop.infra
setenv IFV_HIER_2 des.algo_top.t2_loop\[\*\].stack_loop.infra

run t2_mem_check t2_mem_check
run t2_port_check t2_stack_port_check

setenv IFV_HIER des.algo_top.a1_loop.algo
setenv IFV_HIER_2 des.algo_top.t1_loop\[\*\].align_loop.infra

run pdout_check pdout_check
run t1_port_check t1_port_check

setenv IFV_HIER des.algo_top.a1_loop.algo
setenv IFV_HIER_2 des.algo_top.t2_loop\[\*\].align_loop.infra

run sdout_check sdout_check
run t2_port_check t2_port_check

setenv IFV_HIER des.algo_top.t\*_loop\[\*\].align_loop.infra

run mem_chk_check mem_chk_check
