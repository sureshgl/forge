#!/bin/csh -f

if($#argv == 0) then
    echo "Usage: $0 <top_module_name> [-noob]"
    echo "   this runs only from <cut-dir>/run_sample/<cut-name> dir"
    echo "   -noob : use this if cut was generated with -noob"
    exit (1);
endif
echo "cmdline: $0 $argv"
set TOP=$argv[1]
set NOOB=0
if ($#argv == 2) then
    if ($argv[2] == '-noob') then
        set NOOB=1
    endif
endif
set SCRIPT=`readlink -f "$0"`
set SCRIPTPATH=`dirname "$SCRIPT"`
set BINPATH=`dirname "$0"| sed 's:verilogparser/.*$:verilogparser:'`
set BINDIR=`readlink -f "$BINPATH"`
set IPDIR="$BINDIR/ip_obfuscated"

set TOPV="../../../rtl/${TOP}.v"
set DESLIB=`\ls ${IPDIR}/common/rtl/memoir_design_library*.v`
if ($NOOB == 1) then
   set DESLIB=`\ls ${IPDIR}/memoir_design_library*.v`
endif
echo "design library used: $DESLIB"

set TCLA="+tcl+${SCRIPTPATH}"
set CMDP="iev +64bit -F filelist +define+FORMAL +licq +exit +top+${TOP}"

\rm -fr INCA_libs filelist

echo $SCRIPTPATH
echo $TOPV >! filelist
echo "$DESLIB" >> filelist

alias run '\rm -f \!:2.log; echo $CMDP ${TCLA}/\!:1.tcl -l \!:2.log; $CMDP ${TCLA}/\!:1.tcl -l \!:2.log;'

run show_all show_all

setenv IFV_HIER des.algo_top.a1_loop.algo

run mem_check mem_check
run mem_range_check mem_range_check
run np2_check np2_check
run forward_check forward_check
run sold_fwd_check sold_fwd_check
run rmem_check rmem_check
run vdout_int_check vdout_int_check
run vmem_check vmem_check
run srch_check srch_check
run fakemem_check fakemem_check
run dout_check dout_check

setenv IFV_HIER des.algo_top.t2_loop\[\*\].align_loop.infra?

run t2_align_check t2_align_check
run t2_mem_chk_check t2_mem_chk_check
run t2_align_dout_check t2_align_dout_check

setenv IFV_HIER des.algo_top.t2_loop\[\*\].stack_loop.infra

run stack_check stack_check
run stack_dout_check stack_dout_check

setenv IFV_HIER des.algo_top.t2_loop\[\*\].align_loop.infra?
setenv IFV_HIER_2 des.algo_top.t2_loop\[\*\].stack_loop.infra

run t2_mem_check t2_mem_check
run t2_port_check t2_port_check

setenv IFV_HIER des.algo_top.t1_loop\[\*\].align_loop.infra

run t1_align_check t1_align_check
run t1_mem_chk_check t1_mem_chk_check
run t1_align_dout_check t1_align_dout_check

#setenv IFV_HIER des.algo_top.t1_loop\[\*\].refr_loop.infra
#run prefr_check prefr_check
#run refr_bitcell refr_bitcell

setenv IFV_HIER des.algo_top.a1_loop.algo
setenv IFV_HIER_2 des.algo_top.t1_loop\[\*\].align_loop.infra
setenv IFV_HIER_3 des.algo_top.t1_loop\[\*\].refr_loop.infra
setenv IFV_HIER_4 des.algo_top.t2_loop\[\*\].align_loop.infra?

run pdout_check pdout_check
run pdout_err_check pdout_err_check
run port_check port_check
